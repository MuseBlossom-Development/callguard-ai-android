plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.whispercpp'
    compileSdk 34

    defaultConfig {
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                // Release 빌드 및 최적화 플래그
                arguments(
                        "-DCMAKE_BUILD_TYPE=Release",
                        "-DGGML_SIMD=neon",       // NEON SIMD 활성화
                        "-DGGML_F16_KV=1"         // FP16 가속 활성화
                )

                // GGML_HOME 또는 CLBlast 설정이 필요한 경우
                if (project.hasProperty('GGML_HOME') && project.findProperty('GGML_CLBLAST') == 'ON') {
                    arguments.addAll([
                            "-DGGML_HOME=${project.property('GGML_HOME')}",
                            "-DGGML_CLBLAST=ON",
                            "-DOPENCL_LIB=${project.property('OPENCL_LIB')}",
                            "-DCLBLAST_HOME=${project.property('CLBLAST_HOME')}",
                            "-DOPENCL_ROOT=${project.property('OPENCL_ROOT')}",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_INCLUDE=BOTH",
                            "-DCMAKE_FIND_ROOT_PATH_MODE_LIBRARY=BOTH"
                    ])
                } else if (project.hasProperty('GGML_HOME')) {
                    arguments.add("-DGGML_HOME=${project.property('GGML_HOME')}")
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    ndkVersion "25.2.9519653"
    externalNativeBuild {
        cmake {
            path = file("src/main/jni/whisper/CMakeLists.txt")
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
}